plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version "2.2.2"
}

group = 'org.milkteamc'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jeff-media-public"
        url = "https://repo.jeff-media.com/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = "https://nexus.leonardbausenwein.de/repository/maven-public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT"
    implementation "com.jeff_media:SpigotUpdateChecker:3.0.3"
    compileOnly 'me.clip:placeholderapi:2.11.5'
    implementation "de.cubbossa:Translations:3.0.0"
    implementation "net.kyori:adventure-api:4.11.0"
    implementation "net.kyori:adventure-platform-bukkit:4.1.2"
    implementation "net.kyori:adventure-text-minimessage:4.11.0"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.build {
    dependsOn("shadowJar")
}


tasks.shadowJar {
    archiveFileName = "AutoTreeChop-${version}.jar"
    configurations = [project.configurations.runtimeClasspath]
    minimize()

    relocate 'com.jeff_media.updatechecker', 'org.milkteamc.autotreechop.libs.updatechecker'
    relocate 'net.kyori', 'org.milkteamc.autotreechop.libs'
    relocate 'de.cubbossa', 'org.milkteamc.autotreechop.libs'
}

tasks.jar {
    archiveFileName = "AutoTreeChop-${version}-original.jar"
}

tasks.runServer {
    minecraftVersion("1.20.2")
}

runPaper.folia.registerTask()